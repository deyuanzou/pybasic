stms   : NEWLINE* expr (NEWLINE+ expr)* NEWLINE*

expr   : VAR IDENTIFIER ASSIGN expr
       : cmp ((AND|OR) cmp)*
       : arith

cmp    : arith ((EE|LT|EQ|GT|LTE|GTE) arith)*
       : NOT cmp

arith  : term ((PLUS|MINUS) term)*

term   : factor ((MUL|DIV) factor)*      /* 乘除先于加减 */

factor : (PLUS|MINUS) factor
       : power

power  : call (POW factor)*

call   : atom (LPAREN  (expr (COMMA expr)*)? RPAREN)?

atom   : INT|FLOAT|STRING|IDENTIFIER
       : LPAREN expr RPAREN
       : lst
       : cdt
       : flp
       : dlp
       : fun

lst    : LSQUARE (expr (COMMA expr)*)? RSQUARE

cdt    : IF expr THEN expr
         (ELIF expr THEN expr)*
         (ELSE expr)?

flp    : FOR IDENTIFIER ASSIGN expr TO expr
         (STEP expr)? THEN expr

dlp    : WHILE expr THEN expr

fun    : FUN IDENTIFIER ?
         LPAREN (IDENTIFIER (COMMA IDENTIFIER)*)? RPAREN
         ARROW expr